on spawn of a mob:
	wait 10 tick
	if metadata tag "health" of mob is not set:
		set metadata tag "health" of mob to health of mob
	if metadata tag "name" of mob is not set:
		if display name of mob is set:
			set metadata tag "name" of mob to display name of mob
		else:
			set metadata tag "name" of mob to "%mob%" in strict proper case
	set display name of mob to "&c%metadata tag ""name"" of mob% %metadata tag ""health"" of mob% / %metadata tag ""health"" of mob% &c‚ù§"
	#Could be more efficient
	set {_Level} to metadata tag "name" of mob
	set {_Level::*} to {_Level} split at "]"
	set {_Level2::*} to {_Level::1} split at "["
	set {_Level} to {_Level2::2}
	set {_Level} to {_Level} parsed as number
	{_Level} is set
	{_Level} is not ""
	
	if metadata tag "MobLevel" of mob is not set:
		set metadata tag "MobLevel" of mob to {_Level}

on death of mob:
	clear drops
	set {_luck} to luckStat(attacker)
	set {_name} to metadata tag "name" of victim
	chance of (0.1+({_luck}/10))%:
		set {_item} to player head named "&5Mount Loot Crate"
		add "{SkullOwner:{Id:[I;1049324767,745688397,-1636073904,-1503116338],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2RiY2E0YjY5ZWFmOGRjYjdhYzM3MjgyMjhkZThhNjQ0NDA3ODcwMTMzNDJkZGFhYmMxYjAwZWViOGVlYzFlMiJ9fX0=""}]}}}" to NBT of {_Item}
		add {_item} to {_drops::*}
		loop players in radius 5 around victim:
			message "&dA Rare Drop Has Spawned Near You! &5[Mount Loot Crate]" to loop-player
	drop {_drops::*} at victim
	wait 1 tick
	clear metadata tag "currentDamaged" of victim
	clear metadata tag "name" of victim
	clear metadata tag "health" of victim

on spawn of fish:
	chance of 1%:
		nautilus(event-entity's location)

on spawn of a mob:
	if event-entity is a wandering trader:
		cancel event

function nautilus(loc: location):
	spawn turtle 1 meter above {_loc} with nbt "{NoAI:1,Silent:1}"
	set {_squid} to last spawned turtle
	apply invisibility without particles to {_squid} for 20 days
	set metadata tag "health" of {_squid} to 50
	wait 1 tick
	spawn armor stand at {_loc} with nbt "{NoBasePlate:1, Small:1, Invisible:1, ShowArms:1, DisabledSlots:4144959, Pose:{Head:[0f, 0f, 0f],Body:[0f, 0f, 0f],LeftArm:[280f, 180f, 0f],RightArm:[280f, 180f, 0f],LeftLeg:[0f, 0f, 0f],RightLeg:[0f, 0f, 0f]}}"
	set {_eyes} to last spawned armor stand
	set {_eyes}'s tool to polished blackstone button
	set {_eyes}'s off hand tool to polished blackstone button
	loop 4 times:
		spawn armor stand at {_loc} with nbt "{NoBasePlate:1, Small:1, Invisible:1, ShowArms:1, DisabledSlots:4144959}"
		set {_stand} to last spawned armor stand
		add "{Rotation:[%90*loop-value%f, 0.0f]}" to {_stand}'s nbt
		add "{Pose:{Head:[10f, 0f, 0f],Body:[0f, 0f, 0f],LeftArm:[60f, 50f, 0f],RightArm:[60f, 310f, 0f],LeftLeg:[0f, 0f, 0f],RightLeg:[0f, 0f, 0f]}}" to {_stand}'s nbt
		equip {_stand} with light blue terracotta
		add {_stand} to {_stands::*}
	set {_stands::*}'s tool to trident
	set {_stands::*}'s off hand tool to trident
	add "{NoAI:0}" to {_squid}'s nbt
	while {_squid} is alive:
		if {_up} = true:
			if {_counter} < 20:
				add 1 to {_counter}
			else:
				set {_up} to false
		else:
			if {_counter} > 0:
				remove 1 from {_counter}
			else:
				set {_up} to true
				set {_counter} to 0
		loop size of {_stands::*} times:
			set {_rot} to "Rotation" tag of nbt of {_squid}
			teleport {_eyes} to {_squid}
			teleport {_stands::%loop-value%} to {_squid}
			add "{Rotation:[%{_rot}-180%f, 0.0f]}" to {_eyes}'s nbt
			add "{Pose:{Head:[0f, 0f, 0f],Body:[0f, 0f, 0f],LeftArm:[260f, 180f, 0f],RightArm:[260f, 180f, 0f],LeftLeg:[0f, 0f, 0f],RightLeg:[0f, 0f, 0f]}}" to {_eyes}'s nbt
			set {_rot} to {_rot}+(90*loop-value)
			if {_rot} >= 360:
				set {_rot} to {_rot}-360
			add "{Rotation:[%{_rot}%f, 0.0f]}" to {_stands::%loop-value%}'s nbt
			add "{Pose:{Head:[10f, 0f, 0f],Body:[0f, 0f, 0f],LeftArm:[%90-{_counter}%f, 45f, 180f],RightArm:[%90-{_counter}%f, 315f, 180f],LeftLeg:[0f, 0f, 0f],RightLeg:[0f, 0f, 0f]}}" to {_stands::%loop-value%}'s nbt
		wait 1 tick
		loop mobs and players in radius 5 of {_squid}:
			add loop-entity to {_entities::*}
		loop {_entities::*}:
			if {_entities::*} contains a player:
				if {_duration} is not set:
					set {_duration} to now
					set {_stands::*}'s helmet to air
					set {_stands::*}'s chestplate to air
					set {_stands::*}'s leggings to air
					set {_stands::*}'s boots to air
					set {_stands::*}'s tool to air
					set {_stands::*}'s off hand tool to air
					set {_eyes}'s tool to crimson button
					set {_eyes}'s off hand tool to crimson button
				exit loop
			else if {_entities::*} contains a fish or turtle:
				if {_duration} is not set:
					set {_duration} to now
					loop size of {_stands::*} times:
						equip {_stands::%loop-value-2%} with red terracotta
					set {_eyes}'s tool to crimson button
					set {_eyes}'s off hand tool to crimson button
				set {_vec} to vector from loop-value to {_squid}
				set {_vec2} to vector from {_squid} to loop-value
				add "{Rotation:[%vector yaw of {_vec2} ? 0.0%f, 0.0f]}" to {_squid}'s nbt
				if (distance between {_squid} and loop-value) >= 1:
					push {_squid} {_vec2} at speed 0.1
					show critical hit on loop-value to all players
					if loop-value is a turtle:
						make {_squid} damage loop-value by 40
					else:
						push loop-value {_vec} at speed 0.1
						make {_squid} damage loop-value by 5
				exit loop
			else if (difference between {_duration} and now) >= 1 second:
				delete {_duration}
				loop size of {_stands::*} times:
					equip {_stands::%loop-value-2%} with light blue terracotta
				set {_stands::*}'s tool to trident
				set {_stands::*}'s off hand tool to trident
				set {_eyes}'s tool to polished blackstone button
				set {_eyes}'s off hand tool to polished blackstone button
		delete {_entities::*}
	drop 20 of sunflower named "&e&lGold Coin" at {_squid}
	kill {_stands::*}
	kill {_eyes}

on right click with slime spawn egg:
	if name of player's tool is "&aHungry Caterpillar":
		cancel event
		remove 1 of player's tool from player
		push player upwards at speed 2
		push player backwards at speed 4
		set {_loc} to player's location
		wait 1 second
		hungryCaterpillar({_loc})


every 1 real hour:
	set {_pos} to location(-124, 31, 72, world "Hilly_Woods")
	loop mobs in radius 150 of {_pos} where [metadata tag "name" of input is "&6[25] Hungry Caterpillar &c"]:
		stop
	broadcast ""
	broadcast "&aThe &2Hungry Caterpillar&a has spawned!"
	broadcast ""
	hungryCaterpillar({_pos})

on skript stop:
	loop all players:
		kick loop-player
	loop all mobs and items:
		kill loop-entity
	wait 1 tick
	loop all mobs and items:
		kill loop-entity
	

function hungryCaterpillar(loc: location):
	loop 6 times:
		if loop-value is 1:
			spawn slime at {_loc} with nbt "{Size:4}"
			set metadata tag "health" of last spawned slime to 5000
			set metadata tag "name" of last spawned slime to "&6[25] Hungry Caterpillar &c"
		else:
			spawn slime (loop-value) meters behind {_slime::1} with nbt "{Size:3,Invulnerable:1,Paper.canWander:0b,Bukkit.Aware:0b}"
			set metadata tag "name" of last spawned slime to ""
		set {_slime::%loop-value%} to last spawned slime
	while {_slime::*} is alive:
		wait 5 ticks
		set {_rot} to "Rotation" tag of nbt of {_slime::1}
		add "{Rotation:[%random integer between ({_rot} - 10) and ({_rot} + 10)%f, 0.0f]}" to {_slime::1}'s nbt
		loop (size of {_slime::*}) times:
			add "{Rotation:[%{_slime::1}'s yaw%f, 0.0f]}" to {_slime::%loop-value + 1%}'s nbt
			teleport {_slime::%loop-value + 1%} 1.5 meters behind {_slime::%loop-value%}
		if {_spit} is true:
			add "{NoAI:0}" to {_slime::1}'s nbt
			delete {_entities::*}
			delete {_randEntity}
			delete {_spit}
			delete {_counter}
			spawn slime at {_slime::1} with nbt "{Size:2}"
			set {_slime} to last spawned slime
			set metadata tag "name" of {_slime} to "&2[3] Blob &a"
			set metadata tag "health" of {_slime} to 10
		else:
			if {_randEntity} is not set:
				loop mobs and players in radius 6 of {_slime::1} where [{_slime::*} does not contain input]:
					loop-entity is not a npc
					add loop-entity to {_entities::*}
				if size of {_entities::*} > 0:
					set {_randEntity} to random element out of {_entities::*}
					set {_vec} to vector from {_slime::1} to {_randEntity}
					set {_yaw} to vector yaw of {_vec}
					set {_pitch} to vector pitch of {_vec}
					add "{Rotation:[%{_yaw}%f, %{_pitch} ? 0%f]}" to {_slime::1}'s nbt
					add "{NoAI:1}" to {_slime::1}'s nbt
			else:
				if {_randEntity} is alive:
					add 1 to {_counter}
					if {_counter} <= (size of {_slime::*}):
						make {_slime::1} damage {_randEntity} by 3*(size of {_slime::*})
						teleport {_randEntity} to {_slime::%{_counter}%}
					else:
						set {_spit} to true
						teleport {_randEntity} to {_slime::1}
						set {_size} to size of {_slime::*}
						push {_randEntity} upwards at speed 2
						push {_randEntity} forwards at speed 4
				else:
					set {_spit} to true
					loop (size of {_slime::*}) times:
						set {_size} to "Size" tag of nbt of {_slime::%loop-value%}					
						if loop-value is 1:
							if {_size} <= 8:
								add "{Size:%{_size}+1%}" to {_slime::%loop-value%}'s nbt
						else:
							if {_size} <= 7:
								add "{Size:%{_size}+1%}" to {_slime::%loop-value%}'s nbt
					if (size of {_slime::*}) <= 12:
						spawn slime at {_slime::1} with nbt "{Size:3,Invulnerable:1,Paper.canWander:0b,Bukkit.Aware:0b}"
						add last spawned slime to {_slime::*}
				loop (size of {_slime::*}) times:
					reset {_slime::%loop-value + 1%}'s display name
	set {_slime::*}'s health to 0
	chance of 50%:
		drop slime spawn egg named "&aHungry Caterpillar" with lore "&2Click to Spawn" and "&2Unleash the hungry beast!" at {_slime::1}
	loop 60 times:
		spawn slime at {_slime::1} with nbt "{Size:2}"
		set {_slime} to last spawned slime
		set metadata tag "name" of {_slime} to "&2[3] Blob &a"
		set metadata tag "health" of {_slime} to 10
		push {_slime} upwards at speed 2.5
		add {_slime} to {_killallslimes::*}
		wait 1 tick
	wait 2 seconds
	kill {_killallslimes::*}
