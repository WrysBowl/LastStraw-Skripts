command /party [<text>] [<player>]:
	aliases: p
	trigger:
		if {profiles::%player's uuid%::%metadata tag ""profile"" of player%} contains "Red":
			set {_clanColor} to "Red"
		else if {profiles::%player's uuid%::%metadata tag ""profile"" of player%} contains "Blue":
			set {_clanColor} to "Blue"
		set {_arg-1} to arg-1
		set {_arg-2} to arg-2
		if {_arg-1} is not "create" or "invite" or "help" or "leave" or "join" or "info" or "chat" or "kick":
			set {_arg-1} to "%{_arg-1}%%{_arg-2}%"
		if arg-1 is not set:
			message "&b---------------&2[&aHelp&2]&b---------------"
			message "&a/party invite <player>"
			message "&a/party leave"
			message "&a/party join <player>"
			message "&a/party info"
			message "&a/party chat"
		if {_arg-1} is "invite":
			if arg-2 is set:
				if {profiles::%arg-2's uuid%::%metadata tag ""profile"" of arg-2%} contains {_clanColor}:
					if metadata tag "party" of arg-2 is not set:
						if "%metadata tag ""invite"" of arg-2%" does not contain "%player%":
							message "&aInvited %arg-2%&6!"
							send formatted "&aWould you like to join %player%&a's Party?  <tooltip:&a&lYes><cmd:/party join %player%>&a&l[Yes]  <tooltip:&c&lNo>&c&l[No]" to arg-2
							if metadata tag "invite" of arg-2 is not set:
								set metadata tag "invite" of arg-2 to "%player%"
								exit 1 section
							else:
								set metadata tag "invite" of arg-2 to "%metadata tag ""invite"" of arg-2% %player%"
						else:
							message "&cYou already sent an invite to this player!"
					else:
						message "&cThis player is already in a team!"
				else:
					message "&cThis player is not in your clan"
			else:
				message "&cPlease enter a player. /party invite <player>"
		else if {_arg-1} is "leave":
			if metadata tag "party" of player is set:
				message "&c%player%&c left the party!" to all players where [(metadata tag "party" of input) = (metadata tag "party" of player)]
				clear metadata tag "party" of player
				clear metadata tag "chat" of player
				set {_partyMembers::*} to all players where [(metadata tag "party" of input) = (metadata tag "party" of player)]
				if size of {_partyMembers::*} is 1:
					message "&cYou were the only person in the party, so it has been disbanded." to all players where [(metadata tag "party" of input) = (metadata tag "party" of player)]
					clear metadata tag "party" of {_partyMembers::*}
					clear {_partyMembers::*}'s scoreboard
					stop
				if "%{_partySize::*}%" does not contain "%metadata tag ""party"" of {_partyMembers::*}%":
					set metadata tag "party" of {_partyMembers::*} to a random player out of {_partyMembers::*}
				exit 1 section
			else:
				message "&cYou are not in a party!"
		else if {_arg-1} is "join":
			if arg-2 is set:
				if {profiles::%arg-2's uuid%::%metadata tag ""profile"" of arg-2%} contains {_clanColor}:
					if metadata tag "party" of player is not set:
						set {_inviteList::*} to metadata tag "invite" of player split at " "
						loop size of {_inviteList::*} times:
							if {_inviteList::%loop-value%} is arg-2:
								set metadata tag "party" of player to arg-2
								set metadata tag "party" of arg-2 to arg-2
								message "&a%player%&a joined the party!" to all players where [(metadata tag "party" of input) = (metadata tag "party" of player)]
								clear metadata tag "invite" of player
								clear metadata tag "invite" of arg-2
								exit 4 sections
						message "&cThis player has not invited you!"
					else:
						message "&cYou are already in a party."
				else:
					message "&cThis player is not in your clan"
			else:
				message "&cType /party join <player> to join a party."
		else if {_arg-1} is "info":
			set {_partyMembers::*} to all players where [(metadata tag "party" of input) = (metadata tag "party" of player)]
			if metadata tag "party" of player is not set:
				message "&cYou are not in a team!"
			else:
				message "&b---------------&2[&aInfo&2]&b---------------"
				message "&aYou are in %metadata tag ""party"" of player%&a's party!"
				message "&aTotal Party Members: %size of {_partyMembers::*}%"
				message "&aPlayers: %{_partyMembers::*}%"
		else if {_arg-1} is "chat":
			if metadata tag "party" of player is set:
				if metadata tag "chat" of player is true:
					set metadata tag "chat" of player to "false"
					message "&cParty chat has been toggled off."
					exit 2 sections
				else:
					set metadata tag "chat" of player to true
					message "&aParty chat has been toggled on."
		else if {_arg-1} is "kick":
			if arg-2 is set:
				if (metadata tag "party" of arg-2) = "%player%":
					message "&c%arg-2%&c left the party!" to all players where [(metadata tag "party" of input) = (metadata tag "party" of arg-2)]
					clear metadata tag "party" of arg-2
					clear arg-2's scoreboard
				else:
					message "&cYou are not the leader of this party."
			else:
				message "&cType /party kick <player> to kick a player."
				
function scrollPartyScore(player: player, scroll: object):
	set {_uuid} to {_player}'s uuid
	set {_profile} to metadata tag "profile" of {_player}
	set {_levelCeiling} to rounded up ({level::%{_uuid}%::%{_profile}%}^4/100)
	set {_partyMembers::*} to all players where [(metadata tag "party" of input) = (metadata tag "party" of {_player})]
	set title of {_player}'s scoreboard to "&6&lSiege &7(%player count%&7/%max player count%&7)"
	set line 15 of {_player}'s scoreboard to ""
	set line 14 of {_player}'s scoreboard to "&6Profile: &7%{_player}%"
	if ({exp::%{_uuid}%::%{_profile}%}/{_levelCeiling})*100 >= 500:
		set line 13 of {_player}'s scoreboard to " &7➤ Level: &5%{level::%{_uuid}%::%{_profile}%} ? 0% &5(&d0&d%%&5)"
	else:
		set line 13 of {_player}'s scoreboard to " &7➤ Level: &5%{level::%{_uuid}%::%{_profile}%} ? 0% &5(&d%({exp::%{_uuid}%::%{_profile}%}/{_levelCeiling})*100%&d%%&5)"
	set line 12 of {_player}'s scoreboard to " &7➤ Balance: &a%moneyComma({balance::%{_uuid}%::%{_profile}%}) ? 0%"
	set line 11 of {_player}'s scoreboard to "&6Party: &7%size of {_partyMembers::*}% %metadata tag ""party"" of {_player} ? ""&6&nInvites""%"
	if metadata tag "party" of {_player} is set:
		loop size of {_partyMembers::*} times:
			set {_uuidParty} to {_partyMembers::%loop-value%}'s uuid
			set {_profileParty} to metadata tag "profile" of {_partyMembers::%loop-value%}
			add "&7║ &6%{_partyMembers::%loop-value%}% &5Lvl. %{level::%{_uuidParty}%::%{_profileParty}%}%" to {_scoreLines::*}
			add "&7║ &c%((metadata tag ""health"" of {_partyMembers::%loop-value%})/hpStat({_partyMembers::%loop-value%}))*100%&4%% &b%((metadata tag ""mana"" of {_partyMembers::%loop-value%})/mpStat({_partyMembers::%loop-value%}))*100%&3%%  &8[%{profiles::%{_uuid}%::%{_profile}%}%&8]" to {_scoreLines::*}
		loop 9 times:
			if size of {_scoreLines::*} > 10:
				set {_loopValue} to loop-value + {_scroll}-1
			else:
				set {_loopValue} to loop-value
			if {_scoreLines::%{_loopValue}%} is set:
				set line (11 - (loop-value)) of {_player}'s scoreboard to "%{_scoreLines::%{_loopValue}%}%"
			else:
				set line (11 - (loop-value)) of {_player}'s scoreboard to "&7║"
	else:
		set {_inviteList::*} to metadata tag "invite" of {_player} split at " "
		loop 9 times:
			if size of {_inviteList::*} > 10:
				set {_loopValue} to loop-value + {_scroll} - 1
			else:
				set {_loopValue} to loop-value
			if {_inviteList::%{_loopValue}%} is set:
				set line (11 - (loop-value)) of {_player}'s scoreboard to "&7║ &6%{_loopValue}%. &7%{_inviteList::%{_loopValue}%}% invited you"
			else:
				set line (11 - (loop-value)) of {_player}'s scoreboard to "&7║"
	set line 1 of {_player}'s scoreboard to "&6Character %{_profile} ? 0%: %{profiles::%{_uuid}%::%{_profile}%} ? ""Undetermined""%"
		

on tool change:
	if player is sneaking:
		set {_addNum} to metadata tag "partyShift" of player
		set {_partyMembers::*} to all players where [(metadata tag "party" of input) = (metadata tag "party" of player)]
		set {_oldSlot} to (player's current hotbar slot)
		wait 1 ticks
		if (player's current hotbar slot) > {_oldSlot}:
			if (metadata tag "partyShift" of player) <= ((size of {_partyMembers::*})*2)- (1 + (size of {_partyMembers::*})):
				add 1 to {_addNum}
		else if (metadata tag "partyShift" of player) > 1:
			remove 1 from {_addNum}
		if (player's current hotbar slot) is 8:
			if {_oldSlot} is 0:
				remove 1 from {_addNum}
		else if (player's current hotbar slot) is 0:
			if {_oldSlot} is 8:
				add 1 to {_addNum}
		set metadata tag "partyShift" of player to {_addNum}
		scrollPartyScore(player, (metadata tag "partyShift" of player))